const std = @import("std");
const shared = @import("shared");
const NetworkManager = @import("system/NetworkManager.zig");
const Spawner = @import("system/Spawner.zig");
const Game = @import("system/Game.zig");
const nz = shared.numz;
const Physics = @import("system/Physics.zig");

pub const Info = struct {
    delta_time: f32,
    elapsed_time: f32,
    world: *World,
};

pub const Camera = struct {
    transform: nz.Transform3D(f32) = .{},
    pitch: f32 = 0,
};

pub const Entity = struct {
    pub const Flags = packed struct(u32) {
        transform: bool = false,
        collider: bool = false,
        input: bool = false,
        camera: bool = false,
        planet: bool = false,
        _pad: u27 = 0,
    };

    id: u32 = 0,
    flags: Flags = .{},
    kind: shared.EntityKind = .unknown,

    transform: nz.Transform3D(f32) = .{},
    collider: Physics.Collider = undefined,
    input: shared.net.Command.Input = .{},
    camera: Camera = .{},
    planet: u32 = 0,

    pub fn deinit(self: *Entity, gpa: std.mem.Allocator) void {
        if (self.flags.collider) {
            switch (self.collider.shape) {
                .mesh => |*mesh| {
                    mesh.indices.deinit(gpa);
                    mesh.vertices.deinit(gpa);
                },
                .primitive => {},
            }
        }
    }
};

pub const World = struct {
    mutex: std.Io.Mutex = .init,
    gpa: std.mem.Allocator,
    entities: std.AutoArrayHashMapUnmanaged(u32, Entity) = .empty,
    next_id: u32 = 1,

    pub fn init(gpa: std.mem.Allocator) !@This() {
        return .{ .gpa = gpa };
    }
    pub fn deinit(self: *@This()) void {
        for (self.entities.values()) |*entity| entity.deinit(self.gpa);
        self.entities.deinit(self.gpa);
    }

    pub fn spawn(self: *@This()) !*Entity {
        const id = self.next_id;
        self.next_id += 1;
        try self.entities.put(self.gpa, id, .{ .id = id });
        return self.entities.getPtr(id).?;
    }

    pub fn get(self: *@This(), id: u32) ?*Entity {
        return self.entities.getPtr(id);
    }

    pub fn despawn(self: *@This(), id: u32) bool {
        if (self.entities.getPtr(id)) |entity| entity.deinit(self.gpa);
        return self.entities.swapRemove(id);
    }
};

pub const Context = struct {
    gpa: std.mem.Allocator,
    io: std.Io,
    world: *World,
    network_manager: NetworkManager,
    physics: Physics,
    spawner: Spawner,
    game: Game,
    request_exit: bool = false,

    pub const Data = struct {
        gpa: std.mem.Allocator,
        world: *World,
        io: std.Io,
    };

    pub fn init(self: *@This(), data: *const Data) !void {
        self.* = .{
            .gpa = data.gpa,
            .io = data.io,
            .world = data.world,
            .spawner = undefined,
            .game = undefined,
            .network_manager = undefined,
            .physics = undefined,
        };
        try self.physics.init(data.gpa, data.io);
        try self.spawner.init(data.gpa, data.world, &self.physics);
        try self.game.init(data.gpa, data.world);
        try self.network_manager.init(data.gpa, data.io);
        _ = try self.spawner.spawnPlanet();
    }
    pub fn deinit(self: *@This()) !void {
        self.physics.deinit();
        try self.network_manager.deinit();
        try self.game.deinit();
        self.spawner.deinit();
    }

    pub fn update(self: *@This(), info: *const Info) !void {
        try self.network_manager.update(info, &self.spawner);
        try self.physics.update(info);
        try self.game.update(info, &self.spawner);
        // self.request_exit = true;
        // if (info.elapsed_time > 1) self.request_exit = true;
    }
    fn reload(self: *@This(), pre_reload: bool) !void {
        std.log.debug("before-1", .{});
        try self.physics.reload(pre_reload, self.world);
        try self.network_manager.reload(pre_reload);
        std.log.debug("before-0", .{});
    }
};

comptime {
    _ = ffi;
}

pub const ffi = struct {
    pub const Table = struct {
        systemContextInit: *const fn (*Context, data: *const Context.Data) callconv(.c) void,
        systemContextDeinit: *const fn (*Context) callconv(.c) void,
        systemContextUpdate: *const fn (*Context, data: *const Info) callconv(.c) void,
        systemContextReload: *const fn (*Context, pre_reload: bool) callconv(.c) void,

        pub fn load(dynlib: *std.DynLib) !@This() {
            var self: @This() = undefined;
            inline for (@typeInfo(@This()).@"struct".fields) |field| {
                std.log.debug("Looking up symbol: {s}", .{field.name});
                const ptr = dynlib.lookup(field.type, field.name);
                if (ptr) |p| {
                    @field(self, field.name) = p;
                } else {
                    std.log.err("Failed to lookup symbol: {s}", .{field.name});
                    return error.DynlibLookup;
                }
            }
            return self;
        }
    };

    pub export fn systemContextInit(context: *Context, data: *const Context.Data) void {
        std.log.debug("system context init", .{});
        context.init(data) catch |err| {
            if (@errorReturnTrace()) |trace| std.debug.dumpErrorReturnTrace(trace);
            std.log.err("context init: {s}", .{@errorName(err)});
            return;
        };
    }

    pub export fn systemContextDeinit(context: *Context) void {
        std.log.debug("system context deinit", .{});
        context.deinit() catch |err| {
            if (@errorReturnTrace()) |trace| std.debug.dumpErrorReturnTrace(trace);
            std.log.err("context init: {s}", .{@errorName(err)});
            return;
        };
        context.* = undefined;
    }

    pub export fn systemContextUpdate(context: *Context, info: *const Info) void {
        const result = context.update(info);
        result catch |err| {
            if (@errorReturnTrace()) |trace| std.debug.dumpErrorReturnTrace(trace);
            std.log.err("context update: {any}", .{@errorName(err)});
            return;
        };
    }
    pub export fn systemContextReload(context: *Context, pre_reload: bool) void {
        const result = context.reload(pre_reload);
        result catch |err| {
            if (@errorReturnTrace()) |trace| std.debug.dumpErrorReturnTrace(trace);
            std.log.err("context update: {any}", .{@errorName(err)});
            return;
        };
    }
};
