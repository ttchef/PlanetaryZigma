const std = @import("std");
const c = @import("vulkan");
const Vma = @import("Vma.zig");
const Device = @import("device.zig").Logical;
const Buffer = @import("Buffer.zig");
const check = @import("utils.zig").check;

vk_image: c.VkImage = undefined,
vk_imageview: c.VkImageView = undefined,
vma_allocation: Vma.Allocation = undefined,
extent: c.VkExtent3D = undefined,
format: c.VkFormat = undefined,
mip_mapped: bool = undefined,

pub fn init(
    vma: Vma,
    device: Device,
    format: c.VkFormat,
    extent: c.VkExtent3D,
    image_usages_flags: c.VkImageUsageFlags,
    image_view_mask: c.VkImageAspectFlags,
    mip_mapped: bool,
) !@This() {
    var image_info: c.VkImageCreateInfo = .{
        .sType = c.VK_STRUCTURE_TYPE_IMAGE_CREATE_INFO,
        .pNext = null,
        .imageType = c.VK_IMAGE_TYPE_2D,
        .format = format,
        .extent = extent,
        .mipLevels = 1,
        .arrayLayers = 1,
        .samples = c.VK_SAMPLE_COUNT_1_BIT,
        .tiling = c.VK_IMAGE_TILING_OPTIMAL,
        .usage = image_usages_flags,
    };

    const max: f32 = @floatFromInt(@max(extent.width, extent.height));
    if (mip_mapped) {
        image_info.mipLevels = @as(u32, @intFromFloat(@floor(@log2(max)))) + 1;
    }

    var vma_alloc_info: Vma.c.VmaAllocationCreateInfo = .{
        .usage = Vma.c.VMA_MEMORY_USAGE_GPU_ONLY,
        .requiredFlags = c.VK_MEMORY_PROPERTY_DEVICE_LOCAL_BIT,
    };

    var image: c.VkImage = undefined;
    var vma_image_allocation: Vma.Allocation = undefined;
    _ = Vma.c.vmaCreateImage(
        vma.handle,
        @ptrCast(&image_info),
        &vma_alloc_info,
        @ptrCast(&image),
        &vma_image_allocation,
        null,
    );

    var image_view_info: c.VkImageViewCreateInfo = .{
        .sType = c.VK_STRUCTURE_TYPE_IMAGE_VIEW_CREATE_INFO,
        .viewType = c.VK_IMAGE_VIEW_TYPE_2D,
        .image = image,
        .format = format,
        .subresourceRange = .{
            .baseMipLevel = 0,
            .levelCount = image_info.mipLevels,
            .baseArrayLayer = 0,
            .layerCount = 1,
            .aspectMask = image_view_mask,
        },
    };

    var image_view: c.VkImageView = undefined;
    try check(c.vkCreateImageView(device.handle, &image_view_info, null, &image_view));

    return .{
        .format = format,
        .extent = extent,
        .vk_image = image,
        .vk_imageview = image_view,
        .vma_allocation = vma_image_allocation,
        .mip_mapped = mip_mapped,
    };
}

pub fn deinit(self: *@This(), vulkan_mem_alloc: Vma, device: Device) void {
    c.vkDestroyImageView(device.handle, self.vk_imageview, null);
    Vma.c.vmaDestroyImage(vulkan_mem_alloc.handle, @ptrCast(self.vk_image), self.vma_allocation);
}

pub fn uploadDataToImage(self: *@This(), device: Device, vma: Vma.Allocator, data: anytype) !void {
    const data_size: u32 = self.extent.depth * self.extent.width * self.extent.height * 4;
    const upload_buffer: Buffer = try .init(
        device,
        vma,
        u8,
        data_size,
        c.VK_BUFFER_USAGE_TRANSFER_SRC_BIT,
        .{
            .usage = Vma.c.VMA_MEMORY_USAGE_CPU_TO_GPU,
            .flags = Vma.c.VMA_ALLOCATION_CREATE_MAPPED_BIT,
        },
    );
    defer upload_buffer.deinit(vma);

    @memcpy(
        @as([*]u8, @ptrCast(upload_buffer.info.pMappedData))[0..@intCast(data_size)],
        @as([*]u8, @ptrCast(data))[0..@intCast(data_size)],
    );

    const cmd = try device.beginImmediateCommand();

    var image_barrier: Barrier = .init(cmd, self.vk_image, c.VK_IMAGE_ASPECT_COLOR_BIT);
    image_barrier.transition(
        c.VK_IMAGE_LAYOUT_TRANSFER_DST_OPTIMAL,
        c.VK_PIPELINE_STAGE_TRANSFER_BIT,
        c.VK_ACCESS_MEMORY_WRITE_BIT,
    );

    var copy_region: c.VkBufferImageCopy = .{
        .bufferOffset = 0,
        .bufferRowLength = 0,
        .bufferImageHeight = 0,
        .imageSubresource = .{
            .aspectMask = c.VK_IMAGE_ASPECT_COLOR_BIT,
            .mipLevel = 0,
            .baseArrayLayer = 0,
            .layerCount = 1,
        },
        .imageExtent = self.extent,
    };

    c.vkCmdCopyBufferToImage(
        cmd,
        upload_buffer.buffer,
        self.vk_image,
        c.VK_IMAGE_LAYOUT_TRANSFER_DST_OPTIMAL,
        1,
        &copy_region,
    );

    if (self.mip_mapped) {
        generateMipmaps(self, cmd, self.extent);
    } else {
        image_barrier.transition(
            c.VK_IMAGE_LAYOUT_SHADER_READ_ONLY_OPTIMAL,
            0,
            0,
        );
    }

    try device.endImmediateCommand(cmd);
}

fn generateMipmaps(self: *@This(), cmd_buffer: c.VkCommandBuffer, image_size: c.VkExtent3D) void {
    var size = image_size;
    const mip_levels: usize = @as(usize, @intFromFloat(@floor(@log2(@as(f32, @floatFromInt(@max(size.width, size.height))))))) + 1;

    var mip_levels_barrier: Barrier = .init(cmd_buffer, self.vk_image, c.VK_IMAGE_ASPECT_COLOR_BIT);
    mip_levels_barrier.transitionMipLevel(
        c.VK_IMAGE_LAYOUT_TRANSFER_DST_OPTIMAL,
        c.VK_PIPELINE_STAGE_2_TRANSFER_BIT,
        c.VK_ACCESS_2_TRANSFER_WRITE_BIT,
        @intCast(mip_levels),
        0,
        1,
    );

    for (0..mip_levels) |mip| {
        const half_size: c.VkExtent3D = .{
            .height = size.height / 2,
            .width = size.width / 2,
        };
        mip_levels_barrier.old_layout = c.VK_IMAGE_LAYOUT_TRANSFER_DST_OPTIMAL;
        mip_levels_barrier.src_access = c.VK_ACCESS_2_TRANSFER_WRITE_BIT;
        mip_levels_barrier.src_stage = c.VK_PIPELINE_STAGE_2_TRANSFER_BIT;
        mip_levels_barrier.transitionMipLevel(
            c.VK_IMAGE_LAYOUT_TRANSFER_SRC_OPTIMAL,
            c.VK_PIPELINE_STAGE_2_TRANSFER_BIT,
            c.VK_ACCESS_2_MEMORY_READ_BIT,
            1,
            @intCast(mip),
            c.VK_REMAINING_ARRAY_LAYERS,
        );
        if (mip >= mip_levels - 1) continue;

        var blit_info: c.VkBlitImageInfo2 = .{
            .sType = c.VK_STRUCTURE_TYPE_BLIT_IMAGE_INFO_2,
            .pNext = null,
            .pRegions = &.{
                .sType = c.VK_STRUCTURE_TYPE_IMAGE_BLIT_2,
                .pNext = null,
                .srcOffsets = .{
                    .{},
                    .{
                        .x = @intCast(size.width),
                        .y = @intCast(size.height),
                        .z = 1,
                    },
                },
                .dstOffsets = .{
                    .{},
                    .{
                        .x = @intCast(half_size.width),
                        .y = @intCast(half_size.height),
                        .z = 1,
                    },
                },
                .srcSubresource = .{
                    .aspectMask = c.VK_IMAGE_ASPECT_COLOR_BIT,
                    .baseArrayLayer = 0,
                    .layerCount = 1,
                    .mipLevel = @intCast(mip),
                },
                .dstSubresource = .{
                    .aspectMask = c.VK_IMAGE_ASPECT_COLOR_BIT,
                    .baseArrayLayer = 0,
                    .layerCount = 1,
                    .mipLevel = @intCast(mip + 1),
                },
            },
            .dstImage = self.vk_image,
            .dstImageLayout = c.VK_IMAGE_LAYOUT_TRANSFER_DST_OPTIMAL,
            .srcImage = self.vk_image,
            .srcImageLayout = c.VK_IMAGE_LAYOUT_TRANSFER_SRC_OPTIMAL,
            .filter = c.VK_FILTER_LINEAR,
            .regionCount = 1,
        };

        c.vkCmdBlitImage2(cmd_buffer, &blit_info);

        size = half_size;
    }
    mip_levels_barrier.transitionMipLevel(
        c.VK_IMAGE_LAYOUT_SHADER_READ_ONLY_OPTIMAL,
        c.VK_PIPELINE_STAGE_2_TRANSFER_BIT,
        c.VK_PIPELINE_STAGE_2_TRANSFER_BIT,
        @intCast(mip_levels),
        0,
        1,
    );
}

pub fn copyOntoImage(
    self: @This(),
    cmd: c.VkCommandBuffer,
    dest_image: @This(),
) void {
    var blit_region: c.VkImageBlit2 = .{
        .sType = c.VK_STRUCTURE_TYPE_IMAGE_BLIT_2,
        .pNext = null,
        .srcOffsets = .{ .{}, .{
            .x = @intCast(self.extent.width),
            .y = @intCast(self.extent.height),
            .z = 1,
        } },
        .dstOffsets = .{ .{}, .{
            .x = @intCast(dest_image.extent.width),
            .y = @intCast(dest_image.extent.height),
            .z = 1,
        } },
        .srcSubresource = .{
            .aspectMask = c.VK_IMAGE_ASPECT_COLOR_BIT,
            .baseArrayLayer = 0,
            .layerCount = 1,
            .mipLevel = 0,
        },
        .dstSubresource = .{
            .aspectMask = c.VK_IMAGE_ASPECT_COLOR_BIT,
            .baseArrayLayer = 0,
            .layerCount = 1,
            .mipLevel = 0,
        },
    };

    var blit_info: c.VkBlitImageInfo2 = .{
        .sType = c.VK_STRUCTURE_TYPE_BLIT_IMAGE_INFO_2,
        .pNext = null,
        .dstImage = dest_image.vk_image,
        .dstImageLayout = c.VK_IMAGE_LAYOUT_TRANSFER_DST_OPTIMAL,
        .srcImage = self.vk_image,
        .srcImageLayout = c.VK_IMAGE_LAYOUT_TRANSFER_SRC_OPTIMAL,
        .filter = c.VK_FILTER_LINEAR,
        .regionCount = 1,
        .pRegions = &blit_region,
    };

    c.vkCmdBlitImage2(cmd, &blit_info);
}

pub const Barrier = struct {
    cmd: c.VkCommandBuffer,
    image: c.VkImage,
    aspect_mask: c.VkImageAspectFlags,

    old_layout: c.VkImageLayout = c.VK_IMAGE_LAYOUT_UNDEFINED,
    src_stage: c.VkPipelineStageFlags = c.VK_PIPELINE_STAGE_TOP_OF_PIPE_BIT,
    src_access: c.VkAccessFlags = 0,

    pub fn init(
        cmd: c.VkCommandBuffer,
        image: c.VkImage,
        aspect_mask: c.VkImageAspectFlags,
    ) @This() {
        return .{
            .cmd = cmd,
            .image = image,
            .aspect_mask = aspect_mask,
        };
    }

    pub fn transition(self: *@This(), layout: c.VkImageLayout, stage: c.VkPipelineStageFlags, access: c.VkAccessFlags) void {
        var new: c.VkImageMemoryBarrier = .{
            .sType = c.VK_STRUCTURE_TYPE_IMAGE_MEMORY_BARRIER,
            // .srcStageMask = src_stage,
            .srcAccessMask = self.src_access,
            // .dstStageMask = dst_stage,
            .dstAccessMask = access,
            .oldLayout = self.old_layout,
            .newLayout = layout,
            .srcQueueFamilyIndex = c.VK_QUEUE_FAMILY_IGNORED,
            .dstQueueFamilyIndex = c.VK_QUEUE_FAMILY_IGNORED,
            .image = self.image,
            .subresourceRange = .{
                .aspectMask = self.aspect_mask,
                .baseMipLevel = 0,
                .levelCount = 1,
                .baseArrayLayer = 0,
                .layerCount = 1,
            },
        };
        c.vkCmdPipelineBarrier(self.cmd, self.src_stage, stage, 0, 0, null, 0, null, 1, &new);
        self.*.old_layout = layout;
        self.*.src_stage = stage;
        self.*.src_access = access;
    }

    pub fn transitionMipLevel(
        self: *@This(),
        new_layout: c.VkImageLayout,
        dst_stage: c.VkPipelineStageFlags,
        dst_access: c.VkAccessFlags,
        level_count: u32,
        base_mip_level: u32,
        layer_count: u32,
    ) void {
        var new: c.VkImageMemoryBarrier2 = .{
            .sType = c.VK_STRUCTURE_TYPE_IMAGE_MEMORY_BARRIER_2,
            .srcStageMask = self.src_stage,
            .srcAccessMask = self.src_access,
            .dstStageMask = dst_stage,
            .dstAccessMask = dst_access,
            .oldLayout = self.old_layout,
            .newLayout = new_layout,
            .srcQueueFamilyIndex = c.VK_QUEUE_FAMILY_IGNORED,
            .dstQueueFamilyIndex = c.VK_QUEUE_FAMILY_IGNORED,
            .image = self.image,
            .subresourceRange = .{
                .aspectMask = self.aspect_mask,
                .baseMipLevel = base_mip_level,
                .levelCount = level_count,
                .baseArrayLayer = 0,
                .layerCount = layer_count,
            },
        };
        var dep: c.VkDependencyInfo = .{
            .sType = c.VK_STRUCTURE_TYPE_DEPENDENCY_INFO,
            .pNext = null,
            .imageMemoryBarrierCount = 1,
            .pImageMemoryBarriers = &new,
        };
        c.vkCmdPipelineBarrier2(self.cmd, &dep);
        self.*.old_layout = new_layout;
        self.*.src_stage = dst_stage;
        self.*.src_access = dst_access;
    }
};
