const std = @import("std");
const builtin = @import("builtin");
const shared = @import("shared");
const Info = @import("system.zig").Info;
const yes = @import("yes");
const AssetServer = shared.AssetServer;

inner: Inner,

const Vulkan = @import("Renderer/Vulkan.zig");
pub const Vertex = Vulkan.Vertex;

pub const Inner = *Vulkan;

const YesSurfaceCreateUserData = struct {
    platform: yes.Platform,
    window: *yes.Window,
};

pub fn init(gpa: std.mem.Allocator, asset_server: *AssetServer, platform: yes.Platform, window: *yes.Window) !@This() {
    return switch (builtin.os.tag) {
        else => initVulkan(gpa, asset_server, platform, window),
    };
}

pub fn deinit(self: *@This(), gpa: std.mem.Allocator) void {
    self.inner.deinit(gpa);
    switch (builtin.os.tag) {
        .macos => self.inner.deinit(),
        else => {
            gpa.destroy(self.inner);
        },
    }
}

pub fn update(self: *@This(), info: *const Info) !void {
    try self.inner.update(info);
}

pub fn resize(self: *@This(), gpa: std.mem.Allocator, window: *yes.Window) !void {
    try self.inner.resize(gpa, window.size.width, window.size.height);
}

pub fn initVulkan(gpa: std.mem.Allocator, asset_server: *AssetServer, platform: yes.Platform, window: *yes.Window) !@This() {
    const extensions: []const [*:0]const u8 = switch (builtin.os.tag) {
        .windows => &.{
            "VK_KHR_surface",
            "VK_KHR_win32_surface",
        },
        .macos => &.{
            "VK_KHR_surface",
            "VK_MVK_macos_surface",
        },
        .ios => &.{
            "VK_KHR_surface",
            "VK_MVK_ios_surface",
        },
        .linux, .freebsd, .netbsd, .openbsd => if (builtin.abi == .android) &.{
            "VK_KHR_surface",
            "VK_KHR_android_surface",
        } else switch (window.native(platform)) {
            .wayland => &.{
                Vulkan.c.VK_EXT_DEBUG_UTILS_EXTENSION_NAME,
                Vulkan.c.VK_KHR_SURFACE_EXTENSION_NAME,
                Vulkan.c.VK_KHR_DISPLAY_EXTENSION_NAME,

                "VK_KHR_surface",
                "VK_KHR_display",
                "VK_KHR_wayland_surface",
            },
            .x11 => &.{
                Vulkan.c.VK_EXT_DEBUG_UTILS_EXTENSION_NAME,
                Vulkan.c.VK_KHR_SURFACE_EXTENSION_NAME,
                Vulkan.c.VK_KHR_DISPLAY_EXTENSION_NAME,

                "VK_KHR_surface",
                "VK_KHR_display",
                "VK_KHR_xlib_surface",
                "VK_KHR_xcb_surface",
            },
            else => &.{},
        },
        else => &.{},
    };

    var yes_surface_create_user_data: YesSurfaceCreateUserData = .{ .platform = platform, .window = window };

    const vulkan_renderer: *Vulkan = try .init(gpa, asset_server, .{
        .surface = .{
            .data = @ptrCast(@alignCast(&yes_surface_create_user_data)),
            .init = @ptrCast(&createVulkanSurface),
        },
        .instance = .{
            .extensions = extensions,
            .layers = &.{
                "VK_LAYER_KHRONOS_validation",
            },
        },
        .device = .{
            .extensions = &.{
                Vulkan.c.VK_KHR_DYNAMIC_RENDERING_EXTENSION_NAME,
                Vulkan.c.VK_EXT_EXTENDED_DYNAMIC_STATE_EXTENSION_NAME,
                Vulkan.c.VK_EXT_DESCRIPTOR_BUFFER_EXTENSION_NAME,
                Vulkan.c.VK_KHR_SWAPCHAIN_EXTENSION_NAME,
                Vulkan.c.VK_EXT_SHADER_OBJECT_EXTENSION_NAME,
            },
        },
        .swapchain = .{
            .width = window.size.width,
            .heigth = window.size.height,
        },
    });
    return .{ .inner = vulkan_renderer };
}

fn createVulkanSurface(instance: *Vulkan.c.VkInstance, user_data: *const YesSurfaceCreateUserData) !Vulkan.c.VkSurfaceKHR {
    return @ptrCast(try yes.vulkan.createSurface(user_data.platform, user_data.window, @ptrCast(instance), null, @ptrCast(&Vulkan.c.vkGetInstanceProcAddr)));
}
